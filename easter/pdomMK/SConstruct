# R. Nowak, 2010-03-13   -*- mode: Python; -*-
import os, shutil, platform, re

import SCons.Builder

def copyLibBuilder( target, source, env):
   '''kopiuje biblioteke'''
   shutil.copy( str(source[0]), str(target[0]) )
   return

env = Environment()

#sciezki
env.Append( ENV = {'PATH' : os.environ['PATH'] })
env.Append( BUILDERS = { } )

if(platform.system() == "Linux"):
   env.Append( CPPPATH = ['/usr/include/python2.7'] )
   env.Append( LIBPATH = ['/usr/lib/python2.7'] )

   env.Append( CPPFLAGS = '-Wall -pedantic -pthread' )
   env.Append( LINKFLAGS = '-Wall -pthread' )

   env.Append( LIBS = [ 'boost_python' ] )
elif(platform.system() == "Windows"):
   env.Append( CPPPATH = [ Dir('C:/dev/boost_1_55'), #tutaj zainstalowane naglowki boost
                           Dir('C:/dev/Python27/include') ] ) #tutaj zaistalowane naglowki python
   env.Append( LIBPATH = [ Dir('C:/dev/boost_1_55/stage/lib'), #tutaj sciezka do bibliotek boost
                           Dir('C:/dev/Python27/libs') ] ) #tutaj sciezki do bibliotek python

   env.Append( CPPFLAGS = ' /EHsc /MD /D "WIN32" /D "_CONSOLE" /W4' )
   env.Append( LINKFLAGS = ' /SUBSYSTEM:WINDOWS ' )
else:
   print platform.system() + " not supported"

#build C++ library
cpplib = env.SharedLibrary( target = 'calc', source = ['calc/calc-python.cpp', 'calc/computus.cpp'])
if(platform.system() == "Linux"):
   target = 'app/modules/calc.so'
elif(platform.system() == "Windows"):
   target = 'app/modules/calc.pyd'
env.Command(target, cpplib, copyLibBuilder )

#install client application

env.Install('app/views/default','client/index.html')
env.Install('app/static','client/client.js')
env.Install('app/static','client/jquery-1.10.0.min.js')